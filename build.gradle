/*
 * PersonalFinances build script.
 */

plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}

dependencies {

    implementation 'com.google.guava:guava:28.0-jre'

    implementation 'org.apache.pdfbox:pdfbox:2.0.19'

    def log4jVersion = '2.13.3'
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    
    def lombokVersion = '1.18.24'
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    def junitVersion = '5.7.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    implementation "com.sun.xml.bind:jaxb-impl:3.0.2"
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"

    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
}

application {
    mainClassName = 'com.arihunta.finance.App'
}

test {
    useJUnitPlatform()
}

project.ext {
    jaxbTargetDir = file("src/gen/java")
}

sourceSets.main.java.srcDirs += ['src/gen/java']

configurations {
    xsd2java
}

dependencies {
    xsd2java "com.sun.xml.bind:jaxb-xjc:3.0.2"
    xsd2java "com.sun.xml.bind:jaxb-impl:3.0.2"
    xsd2java "com.sun.xml.bind:jaxb-core:3.0.2"
    xsd2java "com.sun.activation:jakarta.activation:2.0.1"
    xsd2java "jakarta.xml.bind:jakarta.xml.bind-api:3.0.1"
}

task xsd2java() {

    doLast {
        jaxbTargetDir.mkdirs()

        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.xsd2java.asPath)
        ant.jaxbTargetDir = jaxbTargetDir

        ant.xjc(
                destdir: '${jaxbTargetDir}',
                package: 'com.arihunta.finance.ofx',
        ) {
            schema( dir: "src/main/resources/xsd/" ) {
                include( name:'OFX2_Protocol.xsd' )
            }
            arg(value: '-verbose')
        }

    }
}

compileJava.dependsOn xsd2java
